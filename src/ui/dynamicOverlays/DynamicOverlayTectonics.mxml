<?xml version="1.0"?>
<dynamicoverlays:DynamicOverlay xmlns:fx="http://ns.adobe.com/mxml/2009"
                                xmlns:dynamicoverlays="ui.dynamicOverlays.*">

    <fx:Script><![CDATA[
        import events.PayloadEvent;

        import logic.modules.TectonicPlate;

        import mx.events.CollectionEvent;

        import ui.AnchoredMapElement;
        import ui.parts.components.PlateTip;

        private var config:Object = {showPlateTips: false, showBorderTips: true};

        override protected function addEventListeners():void
        {
            signal.addEventListener(PayloadEvent.CHANGE_TECTONICS, onChangeTectonics);
            signal.addEventListener(PayloadEvent.CHANGE_OVERLAY, onChangeOverlay);
        }

        override protected function removeEventListeners():void
        {
            signal.removeEventListener(PayloadEvent.CHANGE_TECTONICS, onChangeTectonics);
            signal.removeEventListener(PayloadEvent.CHANGE_OVERLAY, onChangeOverlay);
        }

        private function onChangeOverlay(event:PayloadEvent):void
        {
            for (var key:String in event.payload)
                config[key] = event.payload[key];

            rebuild();
        }

        private function onChangeTectonics(event:PayloadEvent):void
        {
            var collectionEvent:CollectionEvent = event.payload;
            var plate:TectonicPlate;
            var m:AnchoredMapElement;
            var i:int;
            var t:PlateTip;

            switch (collectionEvent.kind)
            {
                case "add":
                    plate = collectionEvent.items[0];
                    addPlateTip(plate);
                    break;
                case "remove":
                    plate = collectionEvent.items[0];
                    for (i = 0; i < numElements; i++)
                    {
                        m = AnchoredMapElement(getElementAt(i));
                        if (!m.getElementAt(0) is PlateTip)
                            continue;

                        t = PlateTip(m.getElementAt(0));
                        if (t.plate == plate)
                            removeElement(m);
                    }
                    break;
                default:
                    // Update
                    for (i = 0; i < numElements; i++)
                    {
                        m = AnchoredMapElement(getElementAt(i));
                        if (!m.getElementAt(0) is PlateTip)
                            continue;

                        t = PlateTip(m.getElementAt(0));
                        m.anchor = t.plate.origin ? t.plate.origin.point : new Point(0, 0);
                        m.visible = m.includeInLayout = t.plate.origin;
                        t.update();
                    }
                    break;
            }
            
            validate(true);
        }

        private function rebuild():void
        {
            while (numElements > 0)
                removeElementAt(0);

            if (config["showPlateTips"])
                for each (var plate:TectonicPlate in model.tectonicPlates)
                    addPlateTip(plate);

            // todo
            // if (showBorderTips)

            validate(true);
        }

        override protected function populate():void
        {
            super.populate();

            rebuild();
        }

        private function addPlateTip(plate:TectonicPlate):void
        {
            var plateTip:PlateTip = new PlateTip();
            plateTip.plate = plate;

            var m:AnchoredMapElement = new AnchoredMapElement();
            m.anchor = plate.origin ? plate.origin.point : new Point(0, 0);
            m.visible = m.includeInLayout = plate.origin;
            m.addElement(plateTip);
            addElement(m);
        }
        ]]></fx:Script>

</dynamicoverlays:DynamicOverlay>

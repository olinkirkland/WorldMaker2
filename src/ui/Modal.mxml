<?xml version="1.0"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009"
         xmlns:s="library://ns.adobe.com/flex/spark"
         creationComplete="onCreationComplete(event)">
    <fx:Script><![CDATA[
        import com.greensock.TweenLite;

        import events.PopupEvent;

        import global.Color;

        import mx.events.FlexEvent;

        import spark.events.ElementExistenceEvent;

        import ui.popups.Popup;

        private function onCreationComplete(event:FlexEvent):void
        {
            // Popup listeners
            PopupManager.instance.addEventListener(PopupEvent.OPEN, function (event:PopupEvent):void
            {
                openPopup(event.popup);
            });

            PopupManager.instance.addEventListener(PopupEvent.CLOSE, function (event:PopupEvent):void
            {
                closePopup();
            });
        }

        private function openPopup(panel:Popup):void
        {
            // Close existing popups
            while (groupPopups.numElements > 0)
                Popup(groupPopups.getElementAt(0)).close();

            groupPopups.addElement(panel);
        }

        private function closePopup():void
        {
            // Close all existing popups
            // Expect only one popup at a time since popups are designed to be modal only
            while (groupPopups.numElements > 0)
                Popup(groupPopups.getElementAt(0)).close();
        }

        private function onPopupAddedOrRemoved(event:ElementExistenceEvent):void
        {
            callLater(function ():void
            {
                popupModalRectangle.includeInLayout = popupModalRectangle.visible = true;
                TweenLite.to(popupModalRectangle, .2, {alpha: groupPopups.numElements > 0 ? 1 : 0, onComplete: validatePopupModalRectangle});
            });

            function validatePopupModalRectangle():void
            {
                popupModalRectangle.includeInLayout = popupModalRectangle.visible = popupModalRectangle.alpha == 1;
            }
        }
        ]]></fx:Script>

    <!--Modal mode rectangle for popups-->
    <s:VGroup id="popupModalRectangle"
              width="100%"
              height="100%"
              visible="false"
              includeInLayout="false"
              alpha="0">
        <s:Rect width="100%"
                height="100%">
            <s:fill>
                <s:SolidColor color="{Color.black}"
                              alpha=".6"/>
            </s:fill>
        </s:Rect>
    </s:VGroup>

    <!--Popup layer-->
    <s:VGroup id="groupPopups"
              elementAdd="onPopupAddedOrRemoved(event)"
              elementRemove="onPopupAddedOrRemoved(event)"
              width="100%"
              height="100%"
              padding="50"
              horizontalAlign="center"
              verticalAlign="middle"/>

</s:Group>
